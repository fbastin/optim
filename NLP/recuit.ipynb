{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "93286e89",
   "metadata": {},
   "source": [
    "\n",
    "# Recherche aléatoire et recuit simulé\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bde1310a",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random\n",
    "using LinearAlgebra\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "50c113f4-58cb-4853-a539-695c0ab174d5",
   "metadata": {},
   "source": [
    "Ce notebook présente une méthode d'optimisation **sans gradient**, à savoir le **recuit simulé**.\n",
    "\n",
    "Cette méthode est adaptée aux problèmes où :\n",
    "- la fonction n’est pas dérivable,\n",
    "- le gradient est coûteux ou indisponible,\n",
    "- la fonction est considérée comme une *boîte noire* mais peu coûteuse à évaluer.\n",
    "\n",
    "Le code est volontairement simple pour se concentrer sur les principes fondamentaux."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d14b9af3-ff14-43c8-96ee-15153ca17fd4",
   "metadata": {},
   "source": [
    "## Principe du recuit simulé\n",
    "\n",
    "Le recuit simulé est une extension probabiliste de la recherche aléatoire.\n",
    "\n",
    "À chaque itération:\n",
    "- si le candidat améliore la fonction, il est accepté\n",
    "- sinon, il peut être accepté avec une certaine probabilité\n",
    "\n",
    "Cette probabilité dépend:\n",
    "- de la dégradation de la fonction\n",
    "- d'un paramètre appelé **température**"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "44f9b751-85bf-4508-be8e-c9e0ba860d8c",
   "metadata": {},
   "source": [
    "## Règle d’acceptation (critère de Metropolis)\n",
    "\n",
    "Soit $\\Delta f = f(x_{\\text{cand}}) - f(x)$. Alors,\n",
    "- si $\\Delta f < 0$: le candidat est accepté;\n",
    "- sinon, il est accepté avec la probabilité\n",
    "$$\n",
    "P = \\exp\\left(-\\frac{\\Delta f}{T}\\right),\n",
    "$$\n",
    "où $T$ est la température. Quand $T$ est élevée, les détériorations sont souvent acceptées. Quand $T \\to 0$, l'algorithme devient une descente stricte."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "21fead1b-612a-41ba-8a26-bac538622087",
   "metadata": {},
   "source": [
    "## Décroissance de la température\n",
    "\n",
    "Nous utilisons une décroissance géométrique:\n",
    "$$\n",
    "T_{k+1} = \\alpha \\, T_k\n",
    "$$\n",
    "avec\n",
    "- $T_0$ température initiale;\n",
    "- $\\alpha \\in (0,1)$.\n",
    "\n",
    "Ce schéma est simple et largement utilisé en pratique."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "82d21ced",
   "metadata": {},
   "outputs": [],
   "source": [
    "function simulated_annealing(f::Function, x0::Vector, nmax::Int;\n",
    "    stepsize::Float64 = 1.0, T0::Float64 = 1.0, alpha::Float64 = 0.99,\n",
    "    patience::Int = nmax, record::Bool = false)\n",
    "\n",
    "    n = length(x0)\n",
    "    x = copy(x0)\n",
    "    value = f(x)\n",
    "    T = T0\n",
    "    no_improve = 0\n",
    "\n",
    "    xs_all = record ? Vector{Vector{Float64}}() : nothing\n",
    "    fs_all = record ? Float64[] : nothing\n",
    "    xs_acc = record ? Vector{Vector{Float64}}() : nothing\n",
    "    fs_acc = record ? Float64[] : nothing\n",
    "\n",
    "    if record\n",
    "        push!(xs_acc, copy(x))\n",
    "        push!(fs_acc, value)\n",
    "    end\n",
    "\n",
    "    for k in 1:nmax\n",
    "        Δx = stepsize * (2rand(n) .- 1)\n",
    "        xcand = x + Δx\n",
    "        cand = f(xcand)\n",
    "        Δf = cand - value\n",
    "\n",
    "        if record\n",
    "            push!(xs_all, xcand)\n",
    "            push!(fs_all, cand)\n",
    "        end\n",
    "\n",
    "        accept = Δf < 0 || rand() < exp(-Δf / T)\n",
    "\n",
    "        if accept\n",
    "            x = xcand\n",
    "            value = cand\n",
    "            no_improve = Δf < 0 ? 0 : no_improve + 1\n",
    "            if record\n",
    "                push!(xs_acc, copy(x))\n",
    "                push!(fs_acc, value)\n",
    "            end\n",
    "        else\n",
    "            no_improve += 1\n",
    "        end\n",
    "\n",
    "        if no_improve ≥ patience\n",
    "            break\n",
    "        end\n",
    "\n",
    "        T *= alpha\n",
    "    end\n",
    "\n",
    "    return record ? (x, value, xs_all, fs_all, xs_acc, fs_acc) : (x, value)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7726e56a-4915-4614-8a72-fe3b6663aa1f",
   "metadata": {},
   "source": [
    "## Exemple en dimension 2\n",
    "\n",
    "Nous considérons la fonction quadratique\n",
    "$$\n",
    "f(x, y) = x^2 + y^2.\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "249090b3",
   "metadata": {},
   "outputs": [],
   "source": [
    "f(x) = x[1]^2 + x[2]^2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f194bced-2957-422d-be76-bf97a1431730",
   "metadata": {},
   "outputs": [],
   "source": [
    "x0 = [3.0, -2.0]\n",
    "\n",
    "xopt, fopt, xs_all, fs_all, xs_acc, fs_acc =\n",
    "    simulated_annealing(f, x0, 5000; record=true)\n",
    "\n",
    "xopt, fopt"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ef35d294-a840-47f9-8666-9c6566b058a5",
   "metadata": {},
   "source": [
    "## Analyse de la convergence\n",
    "\n",
    "Traçons\n",
    "- la valeur de la fonction pour tous les points testés,\n",
    "- la valeur de la fonction pour les points acceptés.\n",
    "\n",
    "Généralement, nous pouvons observer\n",
    "- une forte exploration au début,\n",
    "- puis une stabilisation progressive autour du minimum."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "20fab2a5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(fs_all, label=\"Tous les essais\", alpha=0.4)\n",
    "plot!(fs_acc, label=\"Acceptés\", linewidth=2)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5edb9032-2bab-46e4-bcd3-64419a74a2df",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Grille pour les courbes de niveau\n",
    "xgrid = range(-4, 4, length=200)\n",
    "ygrid = range(-4, 4, length=200)\n",
    "\n",
    "Z = [f([x, y]) for y in ygrid, x in xgrid]\n",
    "\n",
    "# Extraction des coordonnées des itérés\n",
    "X_all = [x[1] for x in xs_all]\n",
    "Y_all = [x[2] for x in xs_all]\n",
    "\n",
    "X_acc = [x[1] for x in xs_acc]\n",
    "Y_acc = [x[2] for x in xs_acc]\n",
    "\n",
    "# Courbes de niveau\n",
    "contour(\n",
    "    xgrid, ygrid, Z;\n",
    "    levels=20,\n",
    "    linewidth=1,\n",
    "    color=:grays,\n",
    "    legend=false\n",
    ")\n",
    "\n",
    "# Points testés (exploration)\n",
    "scatter!(\n",
    "    X_all, Y_all;\n",
    "    markersize=2,\n",
    "    alpha=0.3,\n",
    "    label=\"Points testés\"\n",
    ")\n",
    "\n",
    "# Points acceptés (trajectoire)\n",
    "plot!(\n",
    "    X_acc, Y_acc;\n",
    "    marker=:circle,\n",
    "    markersize=4,\n",
    "    linewidth=2,\n",
    "    label=\"Itérés acceptés\"\n",
    ")\n",
    "\n",
    "# Point initial\n",
    "scatter!(\n",
    "    [x0[1]], [x0[2]];\n",
    "    marker=:star5,\n",
    "    markersize=10,\n",
    "    label=\"Point initial\"\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a93bd6b9-424d-465c-8344-24cba6a6a128",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
