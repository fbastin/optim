{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Implémentation basique du simplexe"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "simplex_method (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "## https://github.com/chkwon/jpor_codes/blob/master/chap5/simplex_method.jl\n",
    "## Slightly modified to avoid the inversion operation\n",
    "\n",
    "using LinearAlgebra, Combinatorics, Printf\n",
    "\n",
    "mutable struct SimplexTableau\n",
    "    z_c     ::Array{Float64} # z_j - c_j\n",
    "    Y       ::Array{Float64} # inv(B) * A\n",
    "    x_B     ::Array{Float64} # inv(B) * b\n",
    "    obj     ::Float64        # c_B * x_B\n",
    "    b_idx   ::Array{Int64}   # indices for basic variables x_B\n",
    "end\n",
    "function is_nonnegative(x::Vector)\n",
    "    return length( x[ x .< 0] ) == 0\n",
    "end\n",
    "\n",
    "function is_nonpositive(z::Array)\n",
    "    return length( z[ z .> 0] ) == 0\n",
    "end\n",
    "\n",
    "function initial_BFS(A, b)\n",
    "    m, n = size(A)\n",
    "\n",
    "    comb = collect(combinations(1:n, m))\n",
    "    for i in length(comb):-1:1\n",
    "      b_idx = comb[i]\n",
    "      B = A[:, b_idx]\n",
    "      if rank(B) == m        \n",
    "        x_B = B\\b\n",
    "        if is_nonnegative(x_B)\n",
    "          return b_idx, x_B, B\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "\n",
    "    error(\"Infeasible\")\n",
    "end\n",
    "\n",
    "function print_tableau(t::SimplexTableau)\n",
    "    m, n = size(t.Y)\n",
    "\n",
    "    hline0 = repeat(\"-\", 6)\n",
    "    hline1 = repeat(\"-\", 7*n)\n",
    "    hline2 = repeat(\"-\", 7)\n",
    "    hline = join([hline0, \"+\", hline1, \"+\", hline2])\n",
    "\n",
    "    println(hline)\n",
    "\n",
    "    @printf(\"%6s|\", \"\")\n",
    "    for j in 1:length(t.z_c)\n",
    "      @printf(\"%6.2f \", t.z_c[j])\n",
    "    end\n",
    "    @printf(\"| %6.2f\\n\", t.obj)\n",
    "\n",
    "    println(hline)\n",
    "\n",
    "    for i in 1:m\n",
    "      @printf(\"x[%2d] |\", t.b_idx[i])\n",
    "      for j in 1:n\n",
    "        @printf(\"%6.2f \", t.Y[i,j])\n",
    "      end\n",
    "      @printf(\"| %6.2f\\n\", t.x_B[i])\n",
    "    end\n",
    "\n",
    "    println(hline)\n",
    "end\n",
    "\n",
    "function pivoting!(t::SimplexTableau)\n",
    "    m, n = size(t.Y)\n",
    "\n",
    "    entering, exiting = pivot_point(t)\n",
    "    println(\"Pivoting: entering = x_$entering, exiting = x_$(t.b_idx[exiting])\")\n",
    "\n",
    "    # Pivoting: exiting-row, entering-column\n",
    "    # updating exiting-row\n",
    "    coef = t.Y[exiting, entering]\n",
    "    t.Y[exiting, :] /= coef\n",
    "    t.x_B[exiting] /= coef\n",
    "\n",
    "    # updating other rows of Y\n",
    "    for i in setdiff(1:m, exiting)\n",
    "      coef = t.Y[i, entering]\n",
    "      t.Y[i, :] -= coef * t.Y[exiting, :]\n",
    "      t.x_B[i] -= coef * t.x_B[exiting]\n",
    "    end\n",
    "\n",
    "    # updating the row for the reduced costs\n",
    "    coef = t.z_c[entering]\n",
    "    t.z_c -= coef * t.Y[exiting, :]'\n",
    "    t.obj -= coef * t.x_B[exiting]\n",
    "\n",
    "    # Updating b_idx\n",
    "    t.b_idx[ findfirst(t.b_idx .== t.b_idx[exiting]) ] = entering\n",
    "end\n",
    "\n",
    "function pivot_point(t::SimplexTableau)\n",
    "    # Finding the entering variable index\n",
    "    entering = findfirst( t.z_c .> 0)[2]\n",
    "    if entering == 0\n",
    "      error(\"Optimal\")\n",
    "    end\n",
    "\n",
    "    # min ratio test / finding the exiting variable index\n",
    "    pos_idx = findall( t.Y[:, entering] .> 0 )\n",
    "    if length(pos_idx) == 0\n",
    "      error(\"Unbounded\")\n",
    "    end\n",
    "    exiting = pos_idx[ argmin( t.x_B[pos_idx] ./ t.Y[pos_idx, entering] ) ]\n",
    "\n",
    "    return entering, exiting\n",
    "end\n",
    "\n",
    "function initialize(c, A, b)\n",
    "    c = Array{Float64}(c)\n",
    "    A = Array{Float64}(A)\n",
    "    b = Array{Float64}(b)\n",
    "\n",
    "    m, n = size(A)\n",
    "\n",
    "    # Finding an initial BFS\n",
    "    b_idx, x_B, B = initial_BFS(A,b)\n",
    "\n",
    "    Y = B\\A\n",
    "    c_B = c[b_idx]\n",
    "    obj = dot(c_B, x_B)\n",
    "\n",
    "    # z_c is a row vector\n",
    "    z_c = zeros(1,n)\n",
    "    n_idx = setdiff(1:n, b_idx)\n",
    "    λ = B\\c_B\n",
    "    z_c[n_idx] = λ' * A[:,n_idx] - c[n_idx]'\n",
    "\n",
    "    return SimplexTableau(z_c, Y, x_B, obj, b_idx)\n",
    "end\n",
    "\n",
    "function is_optimal(t::SimplexTableau)\n",
    "    return is_nonpositive(t.z_c)\n",
    "end\n",
    "\n",
    "function simplex_method(c, A, b)\n",
    "    tableau = initialize(c, A, b)\n",
    "    # print_tableau(tableau)\n",
    "\n",
    "    while !is_optimal(tableau)\n",
    "      pivoting!(tableau)\n",
    "      # print_tableau(tableau)\n",
    "    end\n",
    "\n",
    "    opt_x = zeros(length(c))\n",
    "    opt_x[tableau.b_idx] = tableau.x_B\n",
    "\n",
    "    return opt_x, tableau.obj\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Exemple"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Pivoting: entering = x_1, exiting = x_5\n",
      "Pivoting: entering = x_2, exiting = x_7\n",
      "Pivoting: entering = x_4, exiting = x_6\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "([0.17241379310344832, 1.879310344827586, 0.0, 0.15517241379310345, 0.0, 0.0, 0.0], -5.051724137931035)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "c = [-3; -2; -1; -5; 0; 0; 0]\n",
    "A = [7 3 4 1 1 0 0 ;\n",
    "     2 1 1 5 0 1 0 ;\n",
    "     1 4 5 2 0 0 1 ]\n",
    "b = [7; 3; 8]\n",
    "\n",
    "x, obj = simplex_method(c, A, b)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.0",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
